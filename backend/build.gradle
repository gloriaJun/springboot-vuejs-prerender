group 'gloriaJun'
version projectVersion

// Gradle이 동작하기 위한 repositories와 dependencies를 정의
buildscript {
    // 라이브러리 저장소 정의
    repositories{
        mavenCentral()

    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

sourceCompatibility = javaVersion
targetCompatibility = javaVersion
compileJava.options.encoding = 'UTF-8'

// 라이브러리 저장소 정의
repositories {
    mavenCentral()
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-thymeleaf:${springBootVersion}")
    compile("ch.qos.logback:logback-classic:${logbackVersion}")
    compile("ch.qos.logback:logback-core:${logbackVersion}")
    testCompile("junit:junit")
}

// frontend build & copy to build dir
task resourcesCopy(dependsOn: ':frontend:npmBuild') {
    doLast {
        // copy template
        copy {
            from '../frontend/dist/index.html'
            into 'build/resources/main/templates'
        }
        // copy static
        copy {
            from '../frontend/dist/static'
            into 'build/resources/main/static/static'
        }
    }
}

bootJar {
    baseName = 'spring-boot-vuejs-spa'
    version =  projectVersion

    dependsOn 'resourcesCopy'
}

// gradle 버전 정의 (명시해주는 것을 권고한다고 함)
task wrapper(type: Wrapper) {
    gradleVersion = gradleVersion
}

clean.dependsOn ':frontend:npmClean'
